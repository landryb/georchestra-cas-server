import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        mavenCentral()
        jcenter()
        maven {
            url "https://repo.spring.io/libs-milestone"
            mavenContent { releasesOnly() }
        }
        maven {
            url "https://repo.spring.io/libs-snapshot"
            mavenContent { snapshotsOnly() }
        }
        maven {
            url "https://plugins.gradle.org/m2/"
            mavenContent { releasesOnly() }
        }
    }
    dependencies {
        classpath "de.undercouch:gradle-download-task:${project.gradleDownloadTaskVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.springBootVersion}"
        classpath "io.freefair.gradle:maven-plugin:${project.gradleMavenPluginVersion}"
        classpath "io.freefair.gradle:lombok-plugin:${project.gradleLombokPluginVersion}"

        classpath "com.netflix.nebula:gradle-ospackage-plugin:9.0.0"
    }
}

plugins {
  id 'org.ajoberstar.grgit' version '4.1.1' apply false
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
        mavenContent { snapshotsOnly() }
    }
    maven {
        mavenContent { releasesOnly() }
        url "https://build.shibboleth.net/nexus/content/repositories/releases/"
    }
    maven {
        mavenContent { releasesOnly() }
        url "https://repo.spring.io/milestone/"
    }
    maven {
        url "https://repo.spring.io/snapshot/"
        mavenContent { snapshotsOnly() }
    }
    maven {
        mavenContent { snapshotsOnly() }
        url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
    }
}

def casServerVersion = project.'cas.version'
def casWebApplicationBinaryName = "cas.war"

project.ext."casServerVersion" = casServerVersion
project.ext."casWebApplicationBinaryName" = casWebApplicationBinaryName

apply plugin: "io.freefair.war-overlay"
apply plugin: "io.freefair.lombok"
apply from: rootProject.file("gradle/tasks.gradle")

apply plugin: "war"
apply plugin: "eclipse"
apply plugin: "idea"

apply from: rootProject.file("gradle/springboot.gradle")


apply plugin: "nebula.deb"

dependencies {

    // CAS dependencies/modules may be listed here statically...
    implementation "org.apereo.cas:cas-server-webapp-init:${casServerVersion}"
    implementation "org.apereo.cas:cas-server-support-json-service-registry:${casServerVersion}"
    implementation "org.apereo.cas:cas-server-support-ldap:${casServerVersion}"
    implementation "org.apereo.cas:cas-server-support-pac4j-webflow:${casServerVersion}"
    implementation "org.apereo.cas:cas-server-support-oauth-webflow:${casServerVersion}"
    implementation "org.apereo.cas:cas-server-support-memcached-core:${casServerVersion}"
    implementation "org.apereo.cas:cas-server-support-memcached-ticket-registry:${casServerVersion}"
    implementation "org.apereo.cas:cas-server-support-memcached-spy:${casServerVersion}"

    providedCompile "org.springframework.boot:spring-boot:${springBootVersion}"
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, "seconds"
        cacheDynamicVersionsFor 0, "seconds"

        preferProjectModules()

        def failIfConflict = project.hasProperty("failOnVersionConflict") && Boolean.valueOf(project.getProperty("failOnVersionConflict"))
        if (failIfConflict) {
            failOnVersionConflict()
        }
    }
}

eclipse {
    classpath {
       downloadSources = true
       downloadJavadoc = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

task checkoutDatadir() {
  delete "$buildDir/datadir"
  doLast {
     def grgit = Grgit.clone(dir: "$buildDir/datadir", uri: "https://github.com/georchestra/datadir.git", refToCheckout: datadirRef)
   }
}

task deb(type: Deb) {
    version = getDebianVersion()
    packageName = 'georchestra-cas'
    from ('build/libs/cas.war') {
      into '/usr/share/lib/georchestra-cas'
    }
    from ("$buildDir/datadir/cas") {
      into '/etc/georchestra/cas'
    }
    link "/usr/share/lib/georchestra-cas/cas-generic.war", "/usr/share/lib/georchestra-cas/cas.war"
}

deb.dependsOn build, checkoutDatadir

def getDebianVersion() {
    if (System.properties["debian.version"] != null) {
      return System.properties["debian.version"]
    }

    def commitId = 'git rev-parse --verify --short HEAD'.execute().text.trim()
    def branchName = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()

    "${casServerVersion}.${branchName}.${new Date().format('yyyyMMddhhmm')}~${commitId}"
}
